# Maintainer: Christopher Degawa <ccom@randomderp.com>

_realname=zlib
license=(ZLIB)
url=https://www.zlib.net/
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP (mingw-w64)"
pkgver=1.2.11
pkgrel=0
pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname-git
arch=(any)
provides=("$MINGW_PACKAGE_PREFIX-$_realname")
conflicts=("$MINGW_PACKAGE_PREFIX-$_realname")

makedepends=("$MINGW_PACKAGE_PREFIX-cmake"
    "$MINGW_PACKAGE_PREFIX-ninja")
source=("git+https://github.com/madler/zlib.git#branch=develop"
    https://raw.githubusercontent.com/microsoft/vcpkg/master/ports/zlib/cmake_dont_build_more_than_needed.patch)
sha256sums=(SKIP
    a51534a0a26593b305b209c87ccfc5ba38d3d35370deeda10dcfc407f12ca3af)

pkgver() {
    printf "r%s.%s" "$(git -C "$srcdir/$_realname" rev-list --count HEAD)" "$(git -C "$srcdir/$_realname" rev-parse --short HEAD)"
}

prepare() {
    git -C "$srcdir/$_realname" apply -3 "$srcdir/cmake_dont_build_more_than_needed.patch"
    sed -i "s#-L\${sharedlibdir}##" "$srcdir/$_realname/zlib.pc.in"
}

build() {
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        "$MINGW_PREFIX/bin/cmake" \
        -B "build-$CARCH-shared" \
        -S "$srcdir/$_realname" \
        -GNinja -Wno-dev \
        -DCMAKE_INSTALL_PREFIX="$LOCALDESTDIR" \
        -DSKIP_INSTALL_FILES=ON \
        -DSKIP_BUILD_EXAMPLES=OFF \
        -DBUILD_SHARED_LIBS=ON
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        "$MINGW_PREFIX/bin/cmake" \
        -B "build-$CARCH-static" \
        -S "$srcdir/$_realname" \
        -GNinja -Wno-dev \
        -DCMAKE_INSTALL_PREFIX="$LOCALDESTDIR" \
        -DSKIP_INSTALL_FILES=ON \
        -DSKIP_BUILD_EXAMPLES=OFF \
        -DBUILD_SHARED_LIBS=OFF
    "$MINGW_PREFIX/bin/cmake" --build "build-$CARCH-shared"
    "$MINGW_PREFIX/bin/cmake" --build "build-$CARCH-static"
}

package() {
    DESTDIR="$pkgdir" "$MINGW_PREFIX/bin/cmake" --build "build-$CARCH-shared" --target install
    DESTDIR="$pkgdir" "$MINGW_PREFIX/bin/cmake" --build "build-$CARCH-static" --target install
    mv "$pkgdir$LOCALDESTDIR/bin/libzlib1.dll" "$pkgdir$LOCALDESTDIR/bin/zlib1.dll"
    grep -A 24 '^  Copyright' "$srcdir/$_realname/zlib.h" > "$srcdir/$_realname/LICENSE"
    install -Dm644 "$srcdir/$_realname/LICENSE" "$pkgdir$LOCALDESTDIR/share/licenses/$_realname/LICENSE"
}
