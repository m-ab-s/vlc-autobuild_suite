# Maintainer: Christopher Degawa <ccom@randomderp.com>

_realname=expat
license=(MIT)
url=https://libexpat.github.io/
pkgdesc="An XML parser library (mingw-w64)"
pkgver=2.2.9
pkgrel=0
pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname-git
arch=(any)
provides=("$MINGW_PACKAGE_PREFIX-$_realname")
conflicts=("$MINGW_PACKAGE_PREFIX-$_realname")

makedepends=("$MINGW_PACKAGE_PREFIX-pkg-config"
    "$MINGW_PACKAGE_PREFIX-cmake"
    "$MINGW_PACKAGE_PREFIX-ninja")
source=(git+https://github.com/libexpat/libexpat.git)
sha256sums=(SKIP)

build() {
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        "${MINGW_PREFIX}/bin/cmake" \
        -GNinja \
        -B "$srcdir/build-$MINGW_CHOST" \
        -S "$srcdir/lib$_realname/$_realname" \
        -DCMAKE_INSTALL_PREFIX="$LOCALDESTDIR" \
        -DEXPAT_BUILD_EXAMPLES=OFF \
        -DEXPAT_BUILD_TESTS=OFF \
        -DEXPAT_SHARED_LIBS=OFF \
        -DEXPAT_BUILD_PKGCONFIG=ON
    "${MINGW_PREFIX}/bin/cmake" --build "$srcdir/build-$MINGW_CHOST"
}

package() {
    DESTDIR="$pkgdir" "${MINGW_PREFIX}/bin/cmake" --build "$srcdir/build-$MINGW_CHOST" --target install
    install -Dm644 "$srcdir/lib$_realname/$_realname/COPYING" "$pkgdir$LOCALDESTDIR/share/licenses/$_realname/COPYING"
}
